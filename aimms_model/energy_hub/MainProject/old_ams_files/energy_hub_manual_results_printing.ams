## ams_version=1.0
Model Energy_Hub_Model {
	DeclarationSection sets {
		Set Time {
			SubsetOf: Integers;
			Index: t, s;
			InitialData: data{1 ..8760};
		}
		Set Energy_conversion_technologies {
			Index: conv;
			Definition: data {Heat_pump,Boiler,CHP1,PV,ST,CHP2,CHP3,Grid};
		}
		Set Energy_carriers {
			Index: x;
			Definition: data {Elec,Heat};
		}
	}
	DeclarationSection Load_Parameters {
		Parameter Loads {
			IndexDomain: (t,x);
		}
	}
	DeclarationSection Cost_Parameters {
		Parameter Operating_costs {
			IndexDomain: conv | conv <> 'Grid';
			Definition: data {Heat_pump:0,Boiler:0.09,CHP1:0.09,PV:0,ST:0,CHP2:0.09,CHP3:0.09};
		}
		Parameter Operating_costs_grid {
			Definition:0.24;
		}
		Parameter OMV_costs {
			IndexDomain: conv;
			Definition: data {Heat_pump:0.1,Boiler:0.01,CHP1:0.021,PV:0.06,ST:0.12,CHP2:0.021,CHP3:0.021,Grid:0 };
		}
		Parameter Linear_capital_costs {
			IndexDomain: (x,conv) | Cmatrix(x,conv) > 0;
			Definition: {
			data {(Elec,CHP1):1500,(Elec,PV):437.5,(Elec,CHP2):1500,(Elec,CHP3):1500,(Elec,Grid):0,(Heat,Heat_pump):1000,(Heat,Boiler):200,(Heat,ST):2030}
			};
		}
		Parameter Fixed_capital_costs {
			IndexDomain: (x,conv) | Cmatrix(x,conv) > 0;
			Definition: {
			data {(Elec,CHP1):0,(Elec,PV):0,(Elec,CHP2):0,(Elec,CHP3):0,(Elec,Grid):0,(Heat,Heat_pump):0,(Heat,Boiler):0,(Heat,ST):0}
			};
		}
		Parameter Linear_capital_costs_storage {
			IndexDomain: x;
			Definition: data {Elec:100,Heat:100 };
		}
		Parameter Fixed_capital_costs_storage {
			IndexDomain: x;
			Definition: data {Elec:0,Heat:0 };
		}
		Parameter Electricity_feedin_price {
			IndexDomain: x;
			Definition: data {Elec:0.14 };
		}
		Parameter Interest_rate {
			Definition:0.08;
		}
		Parameter Lifetime {
			IndexDomain: conv | conv <> "Grid";
			Definition: data {Heat_pump:20,Boiler:30,CHP1:20,PV:20,ST:35,CHP2:20,CHP3:20,Grid:0 };
		}
		Parameter Lifetime_storage {
			IndexDomain: x;
			Definition: data {Elec:20,Heat:17 };
		}
		Parameter CRF_tech {
			IndexDomain: conv | conv <> "Grid";
			Definition: Interest_rate/(1-(1/((1+Interest_rate)^(Lifetime(conv)))));
		}
		Parameter CRF_stor {
			IndexDomain: x;
			Definition: Interest_rate/(1-(1/((1+Interest_rate)^(Lifetime_storage(x)))));
		}
	}
	DeclarationSection Technical_Parameters {
		Parameter Cmatrix {
			IndexDomain: (x,conv);
			Definition: { data {(Elec,CHP1):0.3,(Elec,PV):0.14,(Elec,CHP2):0.3,(Elec,CHP3):0.3,(Elec,Grid):1,(Heat,Heat_pump):3.2,(Heat,Boiler):0.94,(Heat,CHP1):0.3,(Heat,ST):0.46,(Heat,CHP2):0.3,(Heat,CHP3):0.3,(Elec,Heat_pump):-1}
			}
		}
		Parameter Max_allowable_capacity {
			IndexDomain: conv | conv ='Heat_pump' OR conv ='Boiler' OR conv ='CHP1' OR conv ='CHP2' OR conv ='CHP3';
			Definition: data {Heat_pump:100,Boiler:100,CHP1:50,CHP2:50,CHP3:50 };
		}
		Parameter Minimum_part_load {
			IndexDomain: (x,conv) | Cmatrix(x,conv) > 0 AND (conv ='Boiler' OR conv ='CHP1' OR conv ='CHP2' OR conv ='CHP3' OR conv ='Heat_pump');
			Definition: { data {(Elec,CHP1):0.5,(Elec,PV):0,(Elec,CHP2):0.5,(Elec,CHP3):0.5,(Elec,Grid):0,(Heat,Heat_pump):0,(Heat,Boiler):0,(Heat,CHP1):0.5,(Heat,ST):0,(Heat,CHP2):0.5,(Heat,CHP3):0.5}
			}
		}
		Parameter Storage_max_charge_rate {
			IndexDomain: x;
			Definition: data {Elec:0.3,Heat:0.25 };
		}
		Parameter Storage_max_discharge_rate {
			IndexDomain: x;
			Definition: data {Elec:0.3,Heat:0.25 };
		}
		Parameter Storage_standing_losses {
			IndexDomain: x;
			Definition: data {Elec:0.001,Heat:0.01 };
		}
		Parameter Storage_charging_efficiency {
			IndexDomain: x;
			Definition: data {Elec:0.9,Heat:0.9 };
		}
		Parameter Storage_discharging_efficiency {
			IndexDomain: x;
			Definition: data {Elec:0.9,Heat:0.9 };
		}
		Parameter Storage_min_SOC {
			IndexDomain: x;
			Definition: data {Elec:0.3,Heat:0 };
		}
		Parameter Storage_maximum_capacity {
			IndexDomain: x;
			Definition: data {Elec:100,Heat:100 };
		}
	}
	DeclarationSection Other_Parameters {
		Parameter Building_roof_area {
			Definition:50;
		}
		Parameter Technology_carbon_factors {
			IndexDomain: (conv);
			Definition: data {Heat_pump:0,Boiler:0.198,CHP1:0.198,PV:0,ST:0,CHP2:0.198,CHP3:0.198,Grid:0.137 };
		}
		Parameter Solar_radiation {
			IndexDomain: t;
		}
		Parameter Big_M {
			Definition: 100000;
		}
	}
	DeclarationSection Variables {
		Variable Input_energy {
			IndexDomain: (t,conv);
			Range: nonnegative;
		}
		Variable Exported_energy {
			IndexDomain: (t,x) | x = "Elec";
			Range: nonnegative;
		}
		Variable Installation {
			IndexDomain: (x,conv) | Cmatrix(x,conv) > 0;
			Range: binary;
		}
		Variable Operation {
			IndexDomain: (t,conv) | (conv ='Heat_pump' OR conv ='Boiler' OR conv ='CHP1' OR conv ='CHP2' OR conv ='CHP3' OR conv ='Grid');
			Range: binary;
		}
		Variable Capacity {
			IndexDomain: (x,conv) | Cmatrix(x,conv) > 0;
			Range: nonnegative;
		}
		Variable Output_energy_electricity  {
			IndexDomain: (t,conv) | Cmatrix('Elec',conv) > 0;
			Range: nonnegative;
			Definition: Input_energy(t,conv) * Cmatrix('Elec',conv);
		}
		Variable Output_energy_heat {
			IndexDomain: (t,conv) | Cmatrix('Heat',conv) > 0;
			Range: nonnegative;
			Definition: Input_energy(t,conv) * Cmatrix('Heat',conv);
		}
	}
	DeclarationSection Storage {
		Variable Storage_input_energy {
			IndexDomain: (t,x);
			Range: nonnegative;
		}
		Variable Storage_output_energy {
			IndexDomain: (t,x);
			Range: nonnegative;
		}
		Variable Storage_SOC {
			IndexDomain: (t,x);
			Range: nonnegative;
		}
		Variable Storage_capacity {
			IndexDomain: x;
			Range: nonnegative;
		}
		Variable Installation_storage {
			IndexDomain: x;
			Range: binary;
		}
	}
	DeclarationSection Objective_fncts {
		Variable Operating_cost_grid {
			Range: free;
			Definition: sum(conv | conv = 'Grid', sum(t, Operating_costs_grid * Input_energy(t,conv)));
		}
		Variable Operating_cost {
			Range: free;
			Definition: sum(conv | conv <> 'Grid', Operating_costs(conv) * sum(t,Input_energy(t,conv)));
		}
		Variable Maintenance_cost {
			Range: nonnegative;
			Definition: sum((t,conv,x), Maintenance_cost_per_timestep(t,conv,x));
		}
		Variable Maintenance_cost_per_timestep {
			IndexDomain: (t,conv,x) | Cmatrix(x,conv) > 0;
			Range: free;
			Definition: Input_energy(t,conv) * Cmatrix(x,conv) * OMV_costs(conv);
		}
		Variable Income_via_exports {
			Range: nonnegative;
			Definition: sum(x, Electricity_feedin_price(x) * sum(t, Exported_energy(t,x)));
		}
		Variable Capital_cost {
			Range: nonnegative;
			Definition: sum((x,conv), (Fixed_capital_costs(x,conv) * Installation(x,conv) + Linear_capital_costs(x,conv) * Capacity(x,conv)) * CRF_tech(conv)) + sum(x,(Fixed_capital_costs_storage(x) * Installation_storage(x) + Linear_capital_costs_storage(x) * Storage_capacity(x)) * CRF_stor(x));
		}
		Variable Total_cost {
			Range: free;
			Definition: Capital_cost + Operating_cost + Operating_cost_grid + Maintenance_cost - Income_via_exports;
		}
		Variable Total_carbon {
			Range: free;
			Definition: sum(conv,Technology_carbon_factors(conv)*sum(t,Input_energy(t,conv)));
		}
	}
	DeclarationSection Energy_conversion_constraints {
		Constraint Load_balance_constraint {
			IndexDomain: (t,x);
			Definition: sum(conv, Input_energy(t,conv) * Cmatrix(x,conv)) + Storage_output_energy(t,x) - Storage_input_energy(t,x) = Loads(t,x) + Exported_energy(t,x);
		}
		Constraint Capacity_constraint {
			IndexDomain: (t,x,conv) | Cmatrix(x,conv) > 0 AND (conv ='Boiler' OR conv ='CHP1' OR conv ='CHP2' OR conv ='CHP3' OR conv ='Heat_pump');
			Definition: Input_energy(t,conv) * Cmatrix(x,conv) <= Capacity(x,conv);
		}
		Constraint Maximum_capacity_constraint {
			IndexDomain: (x,conv) | Cmatrix(x,conv) > 0 AND (conv ='Heat_pump' OR conv ='Boiler');
			Definition: Capacity(x,conv) <= Max_allowable_capacity(conv);
		}
		Constraint Dispatch_constraint {
			IndexDomain: (t,x,conv) | Cmatrix(x,conv) > 0 AND (conv ='Heat_pump' OR conv ='Boiler' OR conv ='CHP1' OR conv ='CHP2' OR conv ='CHP3' OR conv ='Grid');
			Definition: Input_energy(t,conv) * Cmatrix(x,conv) <= Big_M * Operation(t,conv);
		}
		Constraint Part_load_constraint {
			IndexDomain: (t,x,conv) | Cmatrix(x,conv) > 0 AND (conv ='Heat_pump' OR conv ='Boiler' OR conv ='CHP1' OR conv ='CHP2' OR conv ='CHP3' OR conv ='Grid');
			Definition: Input_energy(t,conv) * Cmatrix(x,conv) + Big_M * (1 - Operation(t,conv)) >= Minimum_part_load(x,conv) * Capacity(x,conv);
		}
		Constraint Solar_input_constraint {
			IndexDomain: (t,conv,x) | Cmatrix(x, conv) > 0 AND (conv ='PV' OR conv ='ST');
			Definition: Input_energy(t,conv) = Solar_radiation(t) * Capacity(x,conv) / 1000;
		}
		Constraint Roof_area_constraint {
			Definition:Capacity('Elec','PV')+Capacity('Heat','ST') <= Building_roof_area;
		}
		Constraint Installation_constraint {
			IndexDomain: (x,conv);
			Definition: Capacity(x,conv) <= Big_M * Installation(x,conv);
		}
		Constraint Operation_constraint {
			IndexDomain: (t,x,conv) | Cmatrix(x,conv) > 0 AND (conv ='Heat_pump' OR conv ='Boiler' OR conv ='CHP1' OR conv ='CHP2' OR conv ='CHP3' OR conv ='Grid');
			Definition: Operation(t,conv) <= Installation(x,conv);
		}
		Constraint Electricity_export {
			IndexDomain: (t,x,conv) | x='Elec' AND conv='Grid';
			Definition: Exported_energy(t,x) <= Big_M * (1 - Operation(t,conv));
		}
		Constraint CHP_HTP_constraint {
			IndexDomain: conv | (conv ='CHP1' OR conv ='CHP2' OR conv ='CHP3');
			Definition: Capacity('Heat',conv) = Cmatrix('Heat', conv) / Cmatrix('Elec', conv) * Capacity('Elec',conv);
		}
		Constraint CHP_HTP_constraint2 {
			IndexDomain: conv | (conv ='CHP1' OR conv ='CHP2' OR conv ='CHP3');
			Definition: Installation('Elec',conv) = Installation('Heat',conv);
		}
		Constraint CHP_installation_constraint {
			IndexDomain: conv | (conv ='CHP1' OR conv ='CHP2' OR conv ='CHP3');
			Definition: Capacity('Elec',conv) = Max_allowable_capacity(conv) * Installation('Elec',conv);
		}
	}
	DeclarationSection Storage_constraints {
		Constraint Storage_balance_constraint {
			IndexDomain: (t,x) | t > first(Time);
			Definition: Storage_SOC(t,x) = (1 - Storage_standing_losses(x)) * Storage_SOC(t-1,x) + Storage_charging_efficiency(x) *  Storage_input_energy(t,x) - (1 / Storage_discharging_efficiency(x)) * Storage_output_energy(t,x);
		}
		Constraint Storage_initialization_constraint_electricity1 {
			IndexDomain: (t,x) | t = first(Time) AND (x = 'Elec');
			Definition: Storage_SOC(t,x) = Storage_capacity(x) * Storage_min_SOC(x);
		}
		Constraint Storage_initialization_constrain_heat2 {
			IndexDomain: (t,x) | t = first(Time) AND (x = 'Heat');
			Definition: Storage_SOC(t,x) = Storage_SOC(last(Time),x);
		}
		Constraint Storage_initialization_constraint_heat3 {
			IndexDomain: (t,x) | t = first(Time) AND (x = 'Heat');
			Definition: Storage_output_energy(t,x) = 0;
		}
		Constraint Storage_charging_rate_constraint {
			IndexDomain: (t,x);
			Definition: Storage_input_energy(t,x) <= Storage_max_charge_rate(x) * Storage_capacity(x);
		}
		Constraint Storage_discharging_rate_constraint {
			IndexDomain: (t,x);
			Definition: Storage_output_energy(t,x) <= Storage_max_discharge_rate(x) * Storage_capacity(x);
		}
		Constraint Storage_capacity_constraint {
			IndexDomain: (t,x);
			Definition: Storage_SOC(t,x) <= Storage_capacity(x);
		}
		Constraint Storage_minimum_SOC_constraint {
			IndexDomain: (t,x);
			Definition: Storage_SOC(t,x) >= Storage_capacity(x) * Storage_min_SOC(x);
		}
		Constraint Storage_installation_constraint {
			IndexDomain: x;
			Definition: Storage_capacity(x) <= Big_M * Installation_storage(x);
		}
		Constraint Storage_maximum_capacity_constraint {
			IndexDomain: x;
			Definition: Storage_capacity(x) <= Storage_maximum_capacity(x);
		}
	}
	DeclarationSection Output_vars {
		Variable Energy_demands {
			IndexDomain: (t,x);
			Range: nonnegative;
			Definition: Loads(t,x);
		}
		Variable Operating_cost_per_technology {
			IndexDomain: conv | conv <> 'Grid';
			Range: free;
			Definition: Operating_costs(conv) * sum(t,Input_energy(t,conv));
		}
		Variable Maintenance_cost_per_technology {
			IndexDomain: conv;
			Range: nonnegative;
			Definition: sum((t,x), Maintenance_cost_per_timestep(t,conv,x));
		}
		Variable Capital_cost_per_technology {
			IndexDomain: conv;
			Range: nonnegative;
			Definition: sum(x, (Fixed_capital_costs(x,conv) * Installation(x,conv) + Linear_capital_costs(x,conv) * Capacity(x,conv)) * CRF_tech(conv));
		}
		Variable Total_cost_per_technology {
			IndexDomain: conv | conv <> 'Grid';
			Range: free;
			Definition: Capital_cost_per_technology(conv) + Operating_cost_per_technology(conv) + Maintenance_cost_per_technology(conv);
		}
		Variable Total_cost_grid {
			Range: free;
			Definition: sum(conv | conv = 'Grid',Capital_cost_per_technology(conv) + Operating_cost_grid + Maintenance_cost_per_technology(conv));
		}
		Variable Capital_cost_per_storage {
			IndexDomain: x;
			Range: nonnegative;
			Definition: (Fixed_capital_costs_storage(x) * Installation_storage(x) + Linear_capital_costs_storage(x) * Storage_capacity(x)) * CRF_stor(x);
		}
		Variable Total_cost_per_storage {
			IndexDomain: x;
			Range: free;
			Definition: Capital_cost_per_storage(x);
		}
		Variable Total_carbon_per_technology {
			IndexDomain: conv;
			Range: free;
			Definition: Technology_carbon_factors(conv)*sum(t,Input_energy(t,conv));
		}
		Variable Total_carbon_per_timestep {
			IndexDomain: t;
			Range: free;
			Definition: sum(conv,Technology_carbon_factors(conv)*Input_energy(t,conv));
		}
	}
	DeclarationSection Mathematical_programs {
		MathematicalProgram Cost_minimization {
			Objective: Total_cost;
			Direction: minimize;
			Constraints: AllConstraints;
			Variables: AllVariables;
			Type: Automatic;
		}
	}
	Procedure MainExecution {
		Body: {
			empty AllVariables;
			Capacity('Heat','Heat_pump').relax := 1;
			Capacity('Heat','Boiler').relax := 1;
			Capacity('Elec','Grid').relax := 1;
			Spreadsheet::RetrieveParameter( "electricity_demand.xlsx", Loads(t,'Elec'),"A1:A8760","electricity_demand");
			Spreadsheet::RetrieveParameter( "heating_demand.xlsx", Loads(t,'Heat'),"A1:A8760","heating_demand");
			Spreadsheet::RetrieveParameter( "solar_inputs.xlsx", Solar_radiation(t),"A1:A8760","solar");
			solve Cost_minimization;

			Spreadsheet::CreateWorkbook("results_demands.xlsx","Energy_demands");
			Spreadsheet::AssignTable("results_demands.xlsx",Energy_demands,"B2:C8761","A2:A8761","B1:C1","Energy_demands",0,1,1);
			Spreadsheet::CloseWorkbook("results_demands.xlsx",1);
			
			Spreadsheet::CreateWorkbook("results_conversion.xlsx","Input_energy");
			Spreadsheet::AssignTable("results_conversion.xlsx",Input_energy,"B2:I8761","A2:A8761","B1:I1","Input_energy",0,1,1);
			Spreadsheet::AddNewSheet("results_conversion.xlsx","Exported_energy");
			Spreadsheet::AssignTable("results_conversion.xlsx",Exported_energy,"B2:C8761","A2:A8761","B1:C1","Exported_energy",0,1,1);
			Spreadsheet::AddNewSheet("results_conversion.xlsx","Output_energy_electricity");
			Spreadsheet::AssignTable("results_conversion.xlsx",Output_energy_electricity,"B2:I8761","A2:A8761","B1:I1","Output_energy_electricity",0,1,1);
			Spreadsheet::AddNewSheet("results_conversion.xlsx","Output_energy_heat");
			Spreadsheet::AssignTable("results_conversion.xlsx",Output_energy_heat,"B2:I8761","A2:A8761","B1:I1","Output_energy_heat",0,1,1);
			Spreadsheet::CloseWorkbook("results_conversion.xlsx",1);

			Spreadsheet::CreateWorkbook("results_storage.xlsx","Storage_input_energy");
			Spreadsheet::AddNewSheet("results_storage.xlsx","Storage_output_energy");
			Spreadsheet::AddNewSheet("results_storage.xlsx","Storage_SOC");
			Spreadsheet::AssignTable("results_storage.xlsx",Storage_input_energy,"B2:C8761","A2:A8761","B1:C1","Storage_input_energy",0,1,1);
			Spreadsheet::AssignTable("results_storage.xlsx",Storage_output_energy,"B2:C8761","A2:A8761","B1:C1","Storage_output_energy",0,1,1);
			Spreadsheet::AssignTable("results_storage.xlsx",Storage_SOC,"B2:C8761","A2:A8761","B1:C1","Storage_SOC",0,1,1);
			Spreadsheet::CloseWorkbook("results_storage.xlsx",1);
			
			Spreadsheet::CreateWorkbook("results_capacities.xlsx","Installation");
			Spreadsheet::AddNewSheet("results_capacities.xlsx","Capacity");
			Spreadsheet::AddNewSheet("results_capacities.xlsx","Storage_capacity");
			Spreadsheet::AddNewSheet("results_capacities.xlsx","Installation_storage");
			Spreadsheet::AssignTable("results_capacities.xlsx",Installation,"B2:I3","A2:A3","B1:I1","Installation",0,1,1);
			Spreadsheet::AssignTable("results_capacities.xlsx",Capacity,"B2:I3","A2:A3","B1:I1","Capacity",0,1,1);
			Spreadsheet::AssignTable("results_capacities.xlsx",Storage_capacity,"B1:B2","A1:A2","","Storage_capacity",0,1,3);
			Spreadsheet::AssignTable("results_capacities.xlsx",Installation_storage,"B1:B2","A1:A2","","Installation_storage",0,1,3);
			Spreadsheet::CloseWorkbook("results_capacities.xlsx",1);
			
			Spreadsheet::CreateWorkbook("results_costs.xlsx","Operating_cost_per_technology");
			Spreadsheet::AddNewSheet("results_costs.xlsx","Maintenance_cost_per_technology");
			Spreadsheet::AddNewSheet("results_costs.xlsx","Capital_cost_per_technology");
			Spreadsheet::AddNewSheet("results_costs.xlsx","Total_cost_per_technology");
			Spreadsheet::AddNewSheet("results_costs.xlsx","Operating_cost_grid");
			Spreadsheet::AddNewSheet("results_costs.xlsx","Total_cost_grid");
			Spreadsheet::AddNewSheet("results_costs.xlsx","Capital_cost_per_storage");
			Spreadsheet::AddNewSheet("results_costs.xlsx","Total_cost_per_storage");
			Spreadsheet::AddNewSheet("results_costs.xlsx","Income_via_exports");
			!Spreadsheet::AssignTable("results_costs.xlsx",Operating_cost_per_technology,"B2:B8","A2:A8","B1:B1","Operating_cost_per_technology",0,1,1);
			Spreadsheet::AssignTable("results_costs.xlsx",Operating_cost_per_technology,"B1:B7","A1:A7","","Operating_cost_per_technology",0,1,3);
			Spreadsheet::AssignTable("results_costs.xlsx",Maintenance_cost_per_technology,"B1:B8","A1:A8","","Maintenance_cost_per_technology",0,1,3);
			Spreadsheet::AssignTable("results_costs.xlsx",Capital_cost_per_technology,"B1:B8","A1:A8","","Capital_cost_per_technology",0,1,3);
			Spreadsheet::AssignTable("results_costs.xlsx",Total_cost_per_technology,"B1:B8","A1:A8","","Total_cost_per_technology",0,1,3);
			Spreadsheet::AssignValue("results_costs.xlsx",Operating_cost_grid,"A1:A1","Operating_cost_grid");
			!Spreadsheet::AssignTable("results_costs.xlsx",Operating_cost_grid,"A1:A1","","","Operating_cost_grid",0,3,3);
			!Spreadsheet::AssignTable("results_costs.xlsx",Total_cost_grid,"A1:A1","","","Total_cost_grid",0,3,3);
			Spreadsheet::AssignValue("results_costs.xlsx",Total_cost_grid,"A1:A1","Total_cost_grid");
			Spreadsheet::AssignTable("results_costs.xlsx",Capital_cost_per_storage,"B1:B2","A1:A2","","Capital_cost_per_storage",0,1,3);
			Spreadsheet::AssignTable("results_costs.xlsx",Total_cost_per_storage,"B1:B2","A1:A2","","Total_cost_per_storage",0,1,3);
			!Spreadsheet::AssignTable("results_costs.xlsx",Income_via_exports,"A1:A1","","","Income_via_exports",0,3,3);
			Spreadsheet::AssignValue("results_costs.xlsx",Income_via_exports,"A1:A1","Income_via_exports");
			Spreadsheet::CloseWorkbook("results_costs.xlsx",1);
			
			Spreadsheet::CreateWorkbook("results_emissions.xlsx","Total_carbon_per_technology");
			Spreadsheet::AddNewSheet("results_emissions.xlsx","Total_carbon_per_timestep");
			Spreadsheet::AssignTable("results_emissions.xlsx",Total_carbon_per_technology,"B1:B8","A1:A8","","Total_carbon_per_technology",0,1,3);
			Spreadsheet::AssignTable("results_emissions.xlsx",Total_carbon_per_timestep,"B1:B8760","A1:A8760","","Total_carbon_per_timestep",0,1,3);
			Spreadsheet::CloseWorkbook("results_emissions.xlsx",1);

		}
	}
	Procedure MainTermination {
		Body: {
			return 1;
		}
	}
}
